plugins {
	id "java"
	id "jacoco"
	id "org.sonarqube" version "3.4.0.2513"
	id "org.springframework.boot" version "2.7.1"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

group = "com.example.study"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set("SWAGGER_VERSION", "2.9.2")
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-data-mongodb"

	implementation "io.springfox:springfox-swagger2:${SWAGGER_VERSION}"
	implementation "io.springfox:springfox-swagger-ui:${SWAGGER_VERSION}"

	implementation "io.micrometer:micrometer-registry-prometheus"

	compileOnly "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
}

jacoco {
	toolVersion = "0.8.8"
	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

tasks.named("test") {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.enabled true
	}
}

sonarqube {
	file("$buildDir/classes").mkdirs();
	properties {
		property "sonar.exclusions", ["**/model/**", "**/exception/**", "**/configuration/**"]
	}
}
